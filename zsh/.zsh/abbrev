# "-*- sh -*-"

typeset -Ag abbrev
abbrev=(
    le    "| less"
    he    "| head"
    ti    "| tail"

    mu    "> /dev/null"

    sr    "| sort"
    sru   "| sort -u"

    gr    "| cgrep"

    wl    "| wc -l"

    xa    "| xargs"

    se    "| sed 's/__CURSOR__//g'"

    fn    "find . -type f -name"
    ty    "-type"
    ex    "-exec echo {} \;"

    lo    "$HOME/local_$arch"

    ">>>" ">&|"
    "<<<" "<(__CURSOR__) "
    "2|" "2>>(__CURSOR__) "
)

# TODO revenir entre les parentheses pour 2>>() et entre les slashe pour sed s///

function abbrev-expand()
{
    local MATCH REPL
    LBUFFER=${LBUFFER%%(#m)[_a-zA-Z0-9<>|]#}
    REPL=$abbrev[$MATCH]
    LBUFFER+=${REPL:-$MATCH}

    if [[ $REPL =~ '__CURSOR__' ]]; then
        RBUFFER=${LBUFFER[(ws:__CURSOR__:)2]}${RBUFFER}
        LBUFFER=${LBUFFER[(ws:__CURSOR__:)1]}
    else
        zle self-insert
    fi
}
zle -N abbrev-expand
