# "-*- sh -*-"

source bookmarks

typeset -U PATH path
typeset -xTU LIBRARY_PATH _library_path
typeset -xTU LD_LIBRARY_PATH _ld_library_path
typeset -xTU C_INCLUDE_PATH _c_include_path
typeset -xTU CPLUS_INCLUDE_PATH _cplus_include_path
typeset -xTU PKG_CONFIG_PATH _pkg_config_path

path=(
    $HOME/.local_$arch/bin/
    $HOME/.local/bin/
    $path
)
_library_path=(
    $HOME/.local_$arch/lib/
    $HOME/.local/lib/
    $HOME/.local/lib64/
    $HOME/.local/lib32/
    $_ld_library_path
)
_ld_library_path=($_library_path)
_c_include_path=(
    $HOME/.local_$arch/include/
    $HOME/.local/include/
    $_c_include_path
)
_cplus_include_path=($_c_include_path)
_pkg_config_path=($HOME/.local_$arch/lib/pkgconfig)

export PYTHONUSERBASE=$HOME/.local_$arch
export PYTHONSTARTUP=$HOME/.pythonrc.py

export GOPATH=$HOME/.go

export CONFIG_SITE=$HOME/config.site

typeset -A namedpathes

namedpathes+=(
    bin ~/.local/bin
    abin ~/.local_$arch/bin
    lib ~/.local/lib
    alib ~/.local_$arch/lib
    share ~/.local/share
    arkonf $ARKONF_DIR
    zarkonf $ZARKONF_DIR
)

for p in ${(k)namedpathes}; do
    eval "alias cd$p=\"cd $namedpathes[$p]\""
    eval "hash -d $p=$namedpathes[$p]"
    ba $p $namedpathes[$p]
done

# (C) 2011 Mikael Magnusson
shadowedpaths() {
    print -l $^commands(e,'() { (( $# > 1 )) && REPLY=$@ } $^path/$REPLY:t(N)',N)
}

path() {
    for file; do
	echo $file:A
    done
}
