# "-*- sh -*-"

autoload -U read-from-minibuffer

sed-line() {
   read-from-minibuffer 'Sed: '
   if [[ -n $REPLY ]]; then
      BUFFER=$(echo $BUFFER | sed $REPLY)
   fi
}
zle -N sed-line

# Insère des quotes lorsqu'un url est collée, backslashe les "?"...
#autoload -U url-quote-magic
#zle -N self-insert url-quote-magic

# Permet d'editer la ligne de commande avec un editeur de texte
autoload -U edit-command-line
zle -N edit-command-line

last-arg() {
    LBUFFER+=' !#$'
    zle complete-word
}
zle -N last-arg

autoload -U insert-files
zle -N insert-files

autoload -U incarg
zle -N incarg

restore-aborted-command() {
    BUFFER=$ZLE_LINE_ABORTED
}
zle -N restore-aborted-command

back-to-last-separator() {
    LBUFFER=$(echo $LBUFFER | sed 's@\([/\.,]\)[^/\.,]\+[/\.,]*\s*$@\1@')
}
zle -N back-to-last-separator

forward-to-last-separator() {
    RBUFFER=$(echo $RBUFFER | sed 's@^\s*[/\.,]*[^/\.,]\+\([/\.,]\)@\1@')
}
zle -N forward-to-last-separator

export WORDCHARS='*?_-.[]~=/&;!#$%^(){}<>+:@'
export FINER_WORDCHARS='?~&!%'

finer-forward-word() {
    local WORDCHARS=$FINER_WORDCHARS
    zle forward-word
}
zle -N finer-forward-word

finer-backward-word() {
    local WORDCHARS=$FINER_WORDCHARS
    zle backward-word
}
zle -N finer-backward-word

finer-delete-word() {
    local WORDCHARS=$FINER_WORDCHARS
    zle delete-word
}
zle -N finer-delete-word

finer-backward-delete-word() {
    local WORDCHARS=$FINER_WORDCHARS
    zle backward-delete-word
}
zle -N finer-backward-delete-word

x-kill-line() {
    echo -n $RBUFFER | xsel -i -b
    zle kill-line
}
zle -N x-kill-line

x-kill-whole-line() {
    echo -n $BUFFER | xsel -i -b
    zle kill-whole-line
}
zle -N x-kill-whole-line

x-yank() {
    LBUFFER=$LBUFFER$(xsel -o -b)
}
zle -N x-yank
